class stateOfTests() {
    static {
        testNumber := 0;
        testsOK    := 0;
        testName   := "unknown";
    }
}

class testStub(nameOfTest, executionResult) {
    this.nameOfTest := nameOfTest;
    this.executionResult := executionResult;
    static {
        is := procedure(referenceResult) {
            stateOfTests.testNumber += 1;

            if (executionResult == referenceResult) {
                stateOfTests.testsOK += 1;
                print("$stateOfTests.testName$: test $stateOfTests.testNumber$ successful ($this.nameOfTest$)");
            } else {
                print("$stateOfTests.testName$: test $stateOfTests.testNumber$ failed ($this.nameOfTest$):");
                print("  reference result is:");
                print("    ", referenceResult);
                print("  but got:");
                print("    ", executionResult);
            }

            return executionResult == referenceResult;
        };
    }
}

class validateForTestCase(nameOfTest) {
    this.nameOfTest := nameOfTest;
    static {
        that := procedure(executionResult) {
            return testStub(this.nameOfTest, executionResult);
        };
    }
}

registerTestSuite := procedure(name) {
    stateOfTests.testName := name;
};

printTestResult := procedure() {
    print("\n$stateOfTests.testName$: $stateOfTests.testsOK$ / $stateOfTests.testNumber$ successful");
    if (stateOfTests.testsOK == stateOfTests.testNumber) {
        print("This group of tests is fine.");
    } else {
        print("This group of tests produced some errors.");
    }
};

